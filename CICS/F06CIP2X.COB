      *================================================================J00001005
       IDENTIFICATION                            DIVISION.              00002005
      *================================================================*00003005
                                                                        00004005
       PROGRAM-ID.  F06CIP2.                                            00005005
                                                                        00006005
      *================================================================*00007005
      *                        TREINAMENTO                             *00008005
      *================================================================*00009005
      *     PROGRAMA....: F06CI02                                      *00010005
      *     PROGRAMADOR.: JOEI LORENTI                                 *00020005
      *     ANALISTA....: IVAN SANCHES                                 *00030005
      *     DATA........: 21/06/2022                                   *00040005
      *----------------------------------------------------------------*00050005
      *     OBJETIVO....: PROGRAMA DE MANIPULACAO DE DADOS DA TABELA   *00070012
      *                   DB2 (FUNC) ATRAVES DE MAPA BMS (F06CIM1)     *00070112
      *                                                                *00070205
      *----------------------------------------------------------------*00070305
      *     ARQUIVOS....:                                              *00070405
      *       DDNAME               I/O                 INCLUDE/BOOK    *00070505
      *----------------------------------------------------------------*00070605
      *     BASE DE DADOS:                                             *00070705
      *       DDNAME               I/O                 INCLUDE/BOOK    *00070805
      *       FOUR001.FUNC          I                    BOOKFUNC       00070905
      *----------------------------------------------------------------*00071005
      *     MODULOS.....:                                              *00071105
      *                                                                *00071205
      *================================================================*00071305
                                                                        00071405
      *================================================================*00071505
       ENVIRONMENT                               DIVISION.              00071605
      *================================================================*00071705
                                                                        00071805
      *----------------------------------------------------------------*00071905
       CONFIGURATION                             SECTION.               00072005
      *----------------------------------------------------------------*00072105
                                                                        00072205
       SPECIAL-NAMES.                                                   00072305
           DECIMAL-POINT                         IS COMMA.              00072405
                                                                        00072505
      *----------------------------------------------------------------*00072605
       INPUT-OUTPUT                              SECTION.               00072705
      *----------------------------------------------------------------*00072805
                                                                        00072905
      *================================================================*00073005
       DATA                                      DIVISION.              00073105
      *================================================================*00073205
                                                                        00073305
      *----------------------------------------------------------------*00073405
       WORKING-STORAGE                           SECTION.               00073505
      *----------------------------------------------------------------*00073605
                                                                        00073705
      *----------------------------------------------------------------*00073705
       01 FILLER                       PIC  X(050)         VALUE        00073705
           '***  FR06CIP2 - INICIO DA AREA DE WORKING   ***'.           00073705
      *----------------------------------------------------------------*00073705
                                                                        00073705
      *----------------------------------------------------------------*00073705
       01 FILLER                       PIC  X(050)         VALUE        00073705
           '***  AREA DO MAPA - F06CIM1   ***'.                         00073705
      *----------------------------------------------------------------*00073705
                                                                        00073705
           COPY F06CIM1.                                                00073805
                                                                        00074008
      *----------------------------------------------------------------*00073705
       01 FILLER                       PIC  X(050)         VALUE        00073705
           '***  DB2  ***'.                                             00073705
      *----------------------------------------------------------------*00073705
           EXEC SQL                                                     00074108
             INCLUDE BOOKFUNC                                           00074208
           END-EXEC.                                                    00074308
                                                                        00074408
           EXEC SQL                                                     00074508
             INCLUDE SQLCA                                              00074608
           END-EXEC.                                                    00074708
                                                                        00074808
      *----------------------------------------------------------------*00073705
       01 FILLER                       PIC  X(050)         VALUE        00073705
           '***  VARIAVEIS DE NULIDADE ***'.                            00073705
      *----------------------------------------------------------------*00073705
                                                                        00073705
       77 WRK-EMAIL-NULL               PIC S9(04) COMP.                 00074908

      *----------------------------------------------------------------*00073705
       01 FILLER                       PIC  X(050)         VALUE        00073705
           '***  VARIAVEIS DE APOIO  ***'.                              00073705
      *----------------------------------------------------------------*00073705

       77 WRK-SQLCODE                  PIC -999.                        00075008

       77 WRK-FLAG                     PIC 9  VALUE ZEROS.              00075008

       77 WRK-SALI                     PIC S9(04).                      00075008
                                                                        00075108
       01 WRK-DATA.                                                     00075208
         05 WRK-ANO                    PIC X(04) VALUE SPACE.           00075310
         05 FILLER                     PIC X(01) VALUE '-' .            00075410
         05 WRK-MES                    PIC X(02) VALUE SPACE.           00075508
         05 FILLER                     PIC X(01) VALUE '-' .            00075610
         05 WRK-DIA                    PIC X(02) VALUE SPACE.           00075710
                                                                        00075805
      *----------------------------------------------------------------*00075905
                                                                        00076005
      *================================================================*00076105
       PROCEDURE                                 DIVISION.              00076205
      *================================================================*00076305
                                                                        00076405
      ******************************************************************00076511
      *                     PROCESSAMENTO PRINCIPAL                    *00076611
      ******************************************************************00076711
      *----------------------------------------------------------------*00076811
       0000-PRINCIPAL                            SECTION.               00076911
      *----------------------------------------------------------------*00077011
                                                                        00077111
           PERFORM 1000-INICIAR.                                        00077211
                                                                        00077311
           PERFORM 2000-PROCESSAR.                                      00077411
                                                                        00077511
             EXEC CICS                                                  00088716
               RETURN TRANSID('T062')                                   00088816
             END-EXEC.                                                  00088916

      *----------------------------------------------------------------*00078005
       0000-99-FIM.                              EXIT.                  00078105
      *----------------------------------------------------------------*00078205
                                                                        00078305
      *----------------------------------------------------------------*00078405
       1000-INICIAR                              SECTION.               00078505
      *----------------------------------------------------------------*00078605
                                                                        00078705
           EXEC CICS SEND                                               00078805
             MAPSET('F06CIM1')                                          00078905
             MAP('MAPTEST')                                             00079005
             ERASE                                                      00079105
             MAPONLY                                                    00079205
           END-EXEC.                                                    00079305
                                                                        00079505
      *----------------------------------------------------------------*00078605
       1000-99-FIM.                              EXIT.                  00079605
      *----------------------------------------------------------------*00078605

      *----------------------------------------------------------------*00078605
       2000-PROCESSAR                            SECTION.               00080005
      *----------------------------------------------------------------*00078605
                                                                        00080105
                                                                        00080205
           EXEC CICS RECEIVE                                            00080305
             MAPSET('F06CIM1')                                          00080405
             MAP('MAPTEST')                                             00080505
             INTO(MAPTESTI)                                             00080605
           END-EXEC.                                                    00080705
                                                                        00080805
           IF EIBAID = '3'                                              00081305
              PERFORM 2100-RETURN                                       00081412
           END-IF.                                                      00081905
                                                                        00082005
           IF EIBAID = '5'                                              00082512
              PERFORM 2200-INSERT                                       00082612
           END-IF.                                                      00086512
                                                                        00086612
           IF EIBAID = '6'                                              00086712
              PERFORM 2300-SELECT                                       00086812
           END-IF.                                                      00086912
                                                                        00087112
           IF EIBAID = '8'                                              00087212
              PERFORM 2400-DELETE                                       00087312
           END-IF.                                                      00087412
                                                                        00087612
           IF EIBAID = '9'                                              00087712
              PERFORM 2500-UPDATE                                       00087812
           END-IF.                                                      00087912
                                                                        00088012
           EXEC CICS SEND                                               00088116
             MAPSET('F06CIM1')                                          00088216
             MAP('MAPTEST')                                             00088316
             DATAONLY                                                   00088416
           END-EXEC.                                                    00088516
                                                                        00089012
      *----------------------------------------------------------------*00078605
       2000-99-FIM.                              EXIT.                  00089112
      *----------------------------------------------------------------*00078605
                                                                        00089312
      *----------------------------------------------------------------*00078605
       2100-RETURN                               SECTION.               00089412
      *----------------------------------------------------------------*00078605
                                                                        00089612
           EXEC CICS                                                    00089712
             RETURN                                                     00089812
           END-EXEC.                                                    00089916
                                                                        00090012
      *----------------------------------------------------------------*00089512
       2100-99-FIM.                              EXIT.                  00090212
      *----------------------------------------------------------------*00089512

      *----------------------------------------------------------------*00089512
       2200-INSERT                               SECTION.               00090612
      *----------------------------------------------------------------*00089512
                                                                        00090812
              MOVE IDI                 TO DB2-ID                        00090912
              MOVE NOMEI               TO DB2-NOME                      00091012
              MOVE SETORI              TO DB2-SETOR                     00091112
              MOVE SALI                TO DB2-SALARIO                   00091212
              MOVE DATADIAI            TO WRK-DIA                       00091312
              MOVE DATAMESI            TO WRK-MES                       00091412
              MOVE DATAANOI            TO WRK-ANO                       00091512
              MOVE WRK-DATA            TO DB2-DATAADM                   00091612
              MOVE EMAILI              TO DB2-EMAIL                     00091712
                                                                        00091812
            EXEC SQL                                                    00091912
              INSERT INTO FOUR001.FUNC                                  00092012
              VALUES (:DB2-ID,                                          00092112
                      :DB2-NOME,                                        00092212
                      :DB2-SETOR,                                       00092312
                      :DB2-SALARIO,                                     00092412
                      :DB2-DATAADM,                                     00092512
                      :DB2-EMAIL )                                      00092612
            END-EXEC                                                    00092712
                                                                        00092812
            EVALUATE SQLCODE                                            00092912
             WHEN 0                                                     00093012
                                                                        00093112
              EXEC SQL                                                  00093212
                COMMIT                                                  00093312
              END-EXEC                                                  00093412
                                                                        00093512
              MOVE 'INSERIDO '         TO MSGO                          00093612
                                                                        00093712
             WHEN -180                                                  00093812
              MOVE SQLCODE             TO WRK-SQLCODE                   00093912
              MOVE 'DATA INVALIDA:'    TO MSGO                          00094012
              MOVE WRK-SQLCODE         TO MSGO(16:4)                    00094112
                                                                        00094212
             WHEN OTHER                                                 00094312
              MOVE SQLCODE             TO WRK-SQLCODE                   00094412
              MOVE 'ERRO....: '        TO MSGO                          00094512
              MOVE WRK-SQLCODE         TO MSGO(11:4)                    00094612
            END-EVALUATE.                                               00094716
                                                                        00094812
      *----------------------------------------------------------------*00094912
       2200-99-FIM.                              EXIT.                  00095012
      *----------------------------------------------------------------*00095112
      *----------------------------------------------------------------*00095212
       2300-SELECT                               SECTION.               00095312
      *----------------------------------------------------------------*00095412
                                                                        00095512
           MOVE IDI                    TO DB2-ID                        00095612
                                                                        00095712
           EXEC SQL                                                     00095812
             SELECT ID,NOME,SETOR,SALARIO,DATAADM,EMAIL                 00095912
               INTO :DB2-ID,:DB2-NOME,:DB2-SETOR,:DB2-SALARIO,          00096012
                    :DB2-DATAADM,:DB2-EMAIL :WRK-EMAIL-NULL             00096112
               FROM FOUR001.FUNC                                        00096212
               WHERE ID = :DB2-ID                                       00096312
               FETCH FIRST ROW ONLY                                     00096412
           END-EXEC                                                     00096512
                                                                        00096612
            EVALUATE SQLCODE                                            00096712
             WHEN 0                                                     00096812
             MOVE DB2-ID               TO IDO                           00096912
             MOVE DB2-NOME             TO NOMEO                         00097012
             MOVE DB2-SETOR            TO SETORO                        00097112
             MOVE DB2-SALARIO          TO SALO                          00097212
             MOVE DB2-DATAADM          TO WRK-DATA                      00097312
               MOVE WRK-DIA            TO DATADIAO                      00097412
               MOVE WRK-MES            TO DATAMESO                      00097512
               MOVE WRK-ANO            TO DATAANOO                      00097612
               IF WRK-EMAIL-NULL       EQUAL -1                         00097712
                 MOVE SPACES           TO EMAILO                        00097812
               ELSE                                                     00097912
                 MOVE  DB2-EMAIL       TO EMAILO                        00098012
               END-IF                                                   00098112
                                                                        00098212
            MOVE 'ENCONTRADO'          TO MSGO                          00098312
                                                                        00098412
           WHEN 100                                                     00098512
            INITIALIZE MAPTESTO                                         00098612
            MOVE 'NAO ENCONTRADO'      TO MSGO                          00098712
                                                                        00098812
           WHEN OTHER                                                   00098912
            MOVE SQLCODE               TO WRK-SQLCODE                   00099012
            MOVE 'ERRO....: '          TO MSGO                          00099112
            MOVE WRK-SQLCODE           TO MSGO(11:4)                    00099212
           END-EVALUATE.                                                00099316
                                                                        00099412
      *----------------------------------------------------------------*00099512
       2300-99-FIM.                              EXIT.                  00099612
      *----------------------------------------------------------------*00099712
                                                                        00099812
      *----------------------------------------------------------------*00099912
       2400-DELETE                               SECTION.               00100012
      *----------------------------------------------------------------*00100112
                                                                        00100212
              MOVE IDI                 TO DB2-ID                        00100312
                                                                        00100412
              EXEC SQL                                                  00100512
                DELETE FROM FOUR001.FUNC WHERE ID = :DB2-ID             00100612
              END-EXEC                                                  00100712
                                                                        00100812
              EVALUATE SQLCODE                                          00100912
                WHEN 0                                                  00101012
                                                                        00101112
                  EXEC SQL                                              00101212
                    COMMIT                                              00101312
                  END-EXEC                                              00101412
                                                                        00101512
              MOVE 'REGISTRO APAGADO'  TO MSGO.                         00101616
                                                                        00101712
                                                                        00101812
      *----------------------------------------------------------------*00101913
       2500-UPDATE                               SECTION.               00102013
      *----------------------------------------------------------------*00102113

            PERFORM 2510-COMPARAR                                       00102213
                                                                        00102213
             IF WRK-FLAG               EQUAL 1
               PERFORM 2520-ATUALIZAR                                   00102213
             END-IF.

      *----------------------------------------------------------------*00108015
       2500-99-FIM.                              EXIT.                  00108115
      *----------------------------------------------------------------*00108215
                                                                        00102213
      *----------------------------------------------------------------*00101913
       2510-COMPARAR                             SECTION.               00102013
      *----------------------------------------------------------------*00102113
                                                                        00102316
            MOVE IDI                   TO DB2-ID                        00102316
                                                                        00102815
             EXEC SQL                                                     001029
               SELECT ID,NOME,SETOR,SALARIO,DATAADM,EMAIL                 001030
                 INTO :DB2-ID,                                            001031
                      :DB2-NOME,                                          001032
                      :DB2-SETOR,                                         001033
                      :DB2-SALARIO,                                       001034
                      :DB2-DATAADM,                                       001035
                      :DB2-EMAIL :WRK-EMAIL-NULL                          001036
                 FROM FOUR001.FUNC                                        001037
                 WHERE ID = :DB2-ID                                       001038
                 FETCH FIRST ROW ONLY
             END-EXEC                                                     001039
                                                                        00104015
            EVALUATE SQLCODE                                            00104116
             WHEN 0                                                     00104215
               IF  DB2-NOME            NOT EQUAL                        00104316
                   NOMEI               AND                              00104416
                   NOMEL               GREATER ZEROS                    00104516
                   MOVE NOMEI          TO DB2-NOME                      00104616
               END-IF                                                   00104715

               IF  DB2-SETOR           NOT EQUAL                        00104816
                   SETORI              AND                              00104916
                   SETORL              GREATER  ZEROS                   00105016
                   MOVE SETORI         TO DB2-SETOR                     00105116
               END-IF                                                   00105215

             MOVE DB2-SALARIO          TO WRK-SALI
               IF WRK-SALI             NOT EQUAL SALI                   00105316
                                       AND
                  SALL                 GREATER ZEROS
                 MOVE SALI             TO DB2-SALARIO                   00105416
               END-IF                                                   00105715

              MOVE DB2-DATAADM         TO WRK-DATA
                IF (WRK-ANO            NOT EQUAL  DATAANOI
                                       AND
                   DATAANOL            GREATER ZEROS ) OR

                   (WRK-MES            NOT EQUAL  DATAMESI
                                       AND
                   DATAMESL            GREATER ZEROS ) OR

                   (WRK-MES            NOT EQUAL  DATADIAI
                                       AND
                   DATADIAL            GREATER ZEROS )

                   MOVE DATAANOI       TO WRK-ANO
                   MOVE DATAMESI       TO WRK-MES
                   MOVE DATADIAI       TO WRK-DIA
                   MOVE WRK-DATA       TO DB2-DATAADM
                END-IF

                IF DB2-EMAIL           NOT EQUAL                        00106316
                   EMAILI              AND                              00106416
                   EMAILL              GREATER ZEROS                    00106516
                   MOVE EMAILI         TO DB2-EMAIL                     00106616
                END-IF                                                  00106715

                MOVE 1 TO WRK-FLAG

               WHEN 100
                 MOVE 'ID NAO EXISTE ' TO MSGO
             END-EVALUATE.                                              00106816
                                                                        00106915
      *----------------------------------------------------------------*00101913
       2520-ATUALIZAR                            SECTION.               00102013
      *----------------------------------------------------------------*00102113

                 EXEC SQL                                               00106915
                   UPDATE FOUR001.FUNC                                  00106915
                   SET NOME    =:DB2-NOME,                              00106915
                       SETOR   =:DB2-SETOR,                             00106915
                       SALARIO =:DB2-SALARIO,                           00106915
                       DATAADM =:DB2-DATAADM,                           00106915
                       EMAIL   =:DB2-EMAIL                              00106915
                   WHERE ID = :DB2-ID                                   00106915
                 END-EXEC                                               00106915
                                                                        00106915
                 IF  SQLCODE EQUAL ZEROS                                00107116
                   EXEC SQL                                             00107416
                     COMMIT                                             00107516
                   END-EXEC                                             00107616
                      MOVE 'REGISTRO ATUALIZADO'    TO MSGO             00107816
                 ELSE
                     MOVE SQLCODE TO WRK-SQLCODE
                      MOVE WRK-SQLCODE TO MSGO
                 END-IF

                 MOVE 0 TO WRK-FLAG.                                    00107915

      *----------------------------------------------------------------*00108015
       2520-99-FIM.                             EXIT.                   00108115
      *----------------------------------------------------------------*00108215
                                                                        00108615
      *----------------------------------------------------------------*00108715
       9000-TRATAR-ERRO                          SECTION.               00108815
      *----------------------------------------------------------------*00108915
                                                                        00109015
      *----------------------------------------------------------------*00109115
       9000-99-FIM.                             EXIT.                   00109215
      *----------------------------------------------------------------*00110012
